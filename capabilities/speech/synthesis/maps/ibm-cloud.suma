profile = "speech/synthesis@1.0"
provider = "ibm-cloud"

map TextToSpeechSynthesis {
  http POST "/instances/{input.instance}/v1/synthesize" {
    security "basic"
    request {
      headers {
        Accept = call mapEncodingToMimeType(audio = input.audio)
        Voice = call mapVoiceOptioinsToVoiceHeaderValue(voice = input.voice)
      }
      body {
        text = input.text
      }
    }

    response 200 {
      audioContent = call base64Encode(input = body)
      map result {
        audioContent = audioContent
      }
    }

    response 404 {
       map error {
        title = body.code_description
        detail = body.error
      }
    }

  }
}

operation mapEncodingToMimeType {
  rate = args.audio.sampleRateHertz

  set {
    result = (() => {
      switch (args.audio.encoding) {
        case 'mp3':
          if(!rate) rate = 22050
          return 'audio/mp3;rate=' + rate

        case 'linear_pcm':
        if(!rate) rate = 8000
          return 'audio/wav;rate=' + rate

        default:
          return 'audio/wav'
      }
    })()
  }

  return result
}

operation mapVoiceOptioinsToVoiceHeaderValue {
  return args.voice.languageCode + '_' + args.voice.name
}

operation base64Encode {
  set {
      result = (() => {
      return args.input.toString('base64')
    })()
  }

  return result
}
