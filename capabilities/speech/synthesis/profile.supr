name = "speech/synthesis"
version = "1.0"

"""
Text to speech synthesis usecase
"""
usecase TextToSpeechSynthesis {
  input {
    text! string
    voice! VoiceOptions
    audio! AudioOptions
  }

  result {
    "Synthesised audio data bytes encoded as specified in the audio params input.
     Base64 encoded."
    audioContent string
  }

  error {
    title! string
    detail string
  }

}

"
Audio encoding
"
model AudioEncoding enum {
  "MP3 audio."
  mp3
  "Uncompressed 16-bit signed little-endian samples (Linear PCM) with WAV header."
  linear_pcm
}

model AudioOptions {
  "The format of the audio byte stream."
  encoding! AudioEncoding
  "The synthesis sample rate (in hertz) for this audio. Selected sample rate has to be supported by selected audio encoding format."
  sampleRateHertz number
}

model VoiceOptions {
  "The language (and potentially also the region) of the voice expressed as a BCP-47 language tag, e.g. 'en-US'."
  languageCode! string
  "The name of the voice."
  name string
}
