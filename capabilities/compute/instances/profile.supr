name = "compute/instances"
version = "1.0.0"

"""
Create Server Instance

Allows to craete server instance in given region, shape and image.
Optionally public ssh keys can be provided to access instance.
"""
usecase CreateInstance unsafe {
  input {
    name!
    shape!
    image!
    region!
    sshKeys
  }

  result Instance
}


"""
Get Instance Information

Returns information about instance and it's status.
"""
usecase InstanceInfo safe {
  input {
    id
  }

  result Instance
}

// ----------------------------------------------------------------------------

"
Unique Instance identifier
This is crated automatical during instance creation
"
field id string

"
Status indicating the state of the instance
"
field status enum {
  new
  provisioning
  active
  rebooting
  shuttingOff
  off
}

"
Human-readable name of the instance
"
field name string

"
Public keys to allow access to instance
"
field sshKeys [string]

"
List of IPv4 and IPv6 addresses
"
field publicIps [string]

"
Region in which the instance should be created

For each region datacenter location is selected according to folowing table:

| region | Prefered location | Fallback location |
|--------|-------------------|-------------------|
|   EU   |     Frankfurt     |      Ireland      |
|   US   |   San Francisco   |      New York     |
"
field region enum {
  EU
  US
}

"
Shape representing CPU, CPU manufacturer and memory
"
field shape enum {
  "
  1vcpu and 1gb memory
  "
  tiny_intel
  tiny_amd

  "
  1vcpu and 2gb memory
  "
  small_intel
  small_amd

  "
  2vcpu and 4gb memory
  "
  medium_intel
  medium_amd

  "
  4vcpu and 8gb memory
  "
  large_intel
  large_amd
}

"
Image with operating system to create intance with
"
field image enum {
  ubuntu_lts
  ubuntu_latest
  debian_9
  debian_10
  fedora_33
  fedora_34
  centos_7
  centos_8
}

"
Datetime string in ISO 8601 representation
"
field createdAt string

// ----------------------------------------------------------------------------

model Instance {
  id
  name
  status
  publicIps
  region
  shape
  image
  createdAt
}
